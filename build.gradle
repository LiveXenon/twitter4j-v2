buildscript {
    // for publishing
    ext {
        groupName = 'jp.takke.twitter4j-v2'
        versionName = '0.1.0'
        versionDescription = 'twitter4j-v2-support Version ' + versionName
        descriptionText = 'A simple wrapper for Twitter API v2 that is designed to be used with Twitter4J.'
        scmUrl = "https://github.com/takke/twitter4j-v2"
        vcsUrl = "git@github.com:takke/twitter4j-v2.git"
    }
}

buildscript {

    ext.kotlin_version = '1.3.72'
    repositories {
        jcenter()
    }
    dependencies {
        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'java-library'
    apply plugin: 'com.jfrog.bintray'

    //--------------------------------------------------
    // for publish
    //--------------------------------------------------
    // $ ./gradlew publishToMavenLocal  # deploy to ~/.m2/repository/jp/takke/twitter4j-v2/twitter4j-v2-support/
    apply plugin: 'maven-publish'

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    def pomConfig = {
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }
        developers {
            developer {
                id "takke"
                name "TAKEUCHI Hiroaki"
                email "takke30@gmail.com"
            }
        }

        scm {
            url scmUrl
        }
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId groupName
                version versionName

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', descriptionText)
                    root.appendNode('name', 'twitter4j-v2')
                    root.appendNode('url', scmUrl)
                    root.children().last() + pomConfig
                }
            }
        }
    }

    //--------------------------------------------------
    // upload to bintray
    //--------------------------------------------------
    // ./gradlew clean bintrayUpload
    bintray {
        user = hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        // false => push upload button on bintray web page to distribute new version manually.
        publish = false

//        configurations = ['MyPublication']

        pkg {
            repo = 'maven'
            name = 'twitter4j-v2'
            userOrg = user
            licenses = ['Apache-2.0']
            labels = ['kotlin']
            vcsUrl = vcsUrl
            version {
                name = versionName
                desc = versionDescription
                vcsTag = versionName
            }
        }
    }

    bintrayUpload.doFirst {
        publications = publishing.publications.collect {
            it.name
        }
    }

    bintrayUpload.dependsOn publishToMavenLocal
}